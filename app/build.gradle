apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.marcochin.teamrandomizer2"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true // Allows us to use VectorDrawableCompat

        // We get a warning from room to provide a schema export directory during compilation if
        // room.schemaLocation is not set. Even though it is not mandatory, it is a good practice
        // to have version history in your codebase and you should commit that file into your
        // version control system (but don't ship it with your app!).
        // https://stackoverflow.com/a/44424908
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def lifecycle_version = "2.1.0"
    def material_version = "1.0.0"
    def dagger_version = "2.24"
    def room_version = "2.2.0-rc01"
    def rxjava_version = "2.2.12"
    def rxandroid_version = "2.1.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Material design widgets
    implementation "com.google.android.material:material:$material_version"

    // Lifecycle (LiveData & ViewModel)
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    // LiveDataReactiveStreams (convert Flowable to LiveData)
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"

    // RxJava & RxAndroid
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // Dagger2
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    // Dagger2 android
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version" // if you use the support libraries
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
}
